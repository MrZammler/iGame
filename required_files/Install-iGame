;******************************************************************************
;
;  _  _____
; (_)/ ____|
;  _| |  __  __ _ _ __ ___   ___
; | | | |_ |/ _` | '_ ` _ \ / _ \
; | | |__| | (_| | | | | | |  __/
; |_|\_____|\__,_|_| |_| |_|\___|
;
; $VER: Install-iGame 1.6 (14.06.2025)
;
; History:
;   1.6   14.06.2025: Added Portuguese language
;   1.5   14.04.2023: Fixed an issue on v2.3.0 release with Icons copy
;   1.4   09.04.2023: Added French and German languages
;                     Added a header message on folder choice page
;   1.3   30.01.2023: Added Turkish language
;   1.2   02.11.2020: Added prompt messages on expert mode
;                     Added the copy of default genres list,
;                       requesting user approval
;                     Request user approval to copy the .info
;   1.1   04.10.2020: Fixed the copy of OS4 version
;                     Added the Italiano catalog
;   1.0   20.03.2020: initial release
;******************************************************************************

;=============================================================================
; Set languages

(set #lang_english             0)
(set #lang_french              1)
(set #lang_german              2)
(set #lang_greek               3)
(set #lang_italiano            4)
(set #lang_portuguese          5)
(set #lang_turkish             6)
(set #lang_maximum             7)

;=============================================================================
; Set defaults

(set #appName "iGame")
(set #defaultDest "RAM:")

;=============================================================================
; default language detection

(set #i 0)
(while (<> @language (select #i "deutsch" @language))
    (set #i (+ #i 1))
)
(if (= #i 1) ; LANGUAGE
    (set #language "english")
    (set #language @language)
)

(set #i 0)
(set #defaultLanguages %10000000000000000)
(while (<> #language (select #i "greek" #language))
    (
        (set #i (+ #i 1))
        (set #defaultLanguages (shiftright #defaultLanguages 1))
    )
)
; always add english as default language
(set #defaultLanguages (bitor #defaultLanguages (shiftleft 1 #lang_english)))

; scan the catalogs directory for supported languages
(set #supportedLanguages %00000000000000000)
(foreach "catalogs" "#?"
    (set #i 0)
    (while (<> @each-name (select #i "english" "french" "german" "greek" "italiano" "portuguese" "turkish" @each-name))
        (set #i (+ #i 1))
    )
    (if (<> #lang_maximum #i)
        (set #supportedLanguages (bitor #supportedLanguages (shiftleft 1 #i)))
    )
)
; always add english as default supported language
(set #supportedLanguages (bitor #supportedLanguages (shiftleft 1 #lang_english)))

;=============================================================================
; English

(if (= #language "english")
    (
        (set #acknowledgeOS (cat "Please acknowledge the automatic\n"
                                 "detection of your operating system:"))
        (set #acknowledgeCPU (cat "Please acknowledge the automatic\n"
                                 "detection of your system CPU:"))
        (set #lang_catalog  (cat "Which catalog file do you want to install?\n"
                                 "(English language is built-in)"))
        (set #destdir  (cat "Please select the folder where you want iGame to be installed. "
                            "A new folder will be created if it doesn't exist."))
        (set #done          (cat "\n"
                                 #appName " has been successfully installed.\n"
                                 "\n"
                                 "Have fun!"))

        (set #destexists_1      "\nThe directory\n\n")
        (set #destexists_2      "\n\nalready contains an iGame drawer. If you continue, the iGame in this drawer will be updated. No backups will be created. The gameslist and repositories files will remain intact.")
    )
)

;=============================================================================
; Checking environment
(set #detectedCPU (database "cpu"))

(set EXEC_ver (getversion "exec.library" (resident)))
(set NEWLIB_ver (getversion "newlib.library" (resident)))
(set AROS_ver (getversion "aros.library" (resident)))

; we use newlib.library to check whether we are running on AmigaOS 4.x
(if (>= NEWLIB_ver (* 4 65536))
		(set #detectedSystem 1)
		(
			; we are not running on AROS
			; now check if we are running on MorphOS (exec.library V50+) or on AmigaOS 3.x
			(if (>= EXEC_ver (* 50 65536))
					(set #detectedSystem 2)
					(set #detectedSystem 0)
			)
		)
)

(set #detectedSystem
    (askchoice
        (prompt #acknowledgeOS)
        (help @askchoice-help)
        (choices "AmigaOS 3.x"
                 "AmigaOS 4.x"
                 "MorphOS 1.x/2.x/3.x"
        )
        (default #detectedSystem)
    )
)
; finally convert the system number into a string for easier understanding
(set #detectedSystem
    (select #detectedSystem "os3" "os4" "mos")
)

(if (= #detectedSystem "os4")
    ; AmigaOS 4.x uses english locale names only
    (
        (set #cat_name_english               "english")
        (set #cat_name_french                "french")
        (set #cat_name_german                "german")
        (set #cat_name_greek                 "greek")
        (set #cat_name_italiano              "italian")
        (set #cat_name_portuguese            "portuguese")
        (set #cat_name_turkish               "turkish")
    )
    ; all other systems use localized locale names
    (
        (set #cat_name_english               "english")
        (set #cat_name_french                "fran�ais")
        (set #cat_name_german                "deutsch")
        (set #cat_name_greek                 "greek")
        (set #cat_name_italiano              "italiano")
        (set #cat_name_portuguese            "portuguese")
        (set #cat_name_turkish               "t�rk�e")
    )
)


;=============================================================================
; Select cpu version
(complete 10)

(if (= #detectedSystem "os3")
	(
		(if (= #detectedCPU "68010")
			(set #selectedCPU 0)
		)
		(if (= #detectedCPU "68020")
			(set #selectedCPU 0)
		)
		(if (= #detectedCPU "68030")
			(set #selectedCPU 1)
		)
		(if (= #detectedCPU "68040")
			(set #selectedCPU 2)
		)
		(if (= #detectedCPU "68060")
			(set #selectedCPU 3)
		)

		(set #selectedCPU
				(askchoice
						(prompt #acknowledgeCPU)
						(help @askchoice-help)
						(choices 	"68000"
									"68030"
									"68040"
									"68060"
						)
						(default #selectedCPU)
				)
		)


		; finally convert the system number into a string for easier understanding
		(set #selectedVersion
				(select #selectedCPU "iGame" "iGame.030" "iGame.040" "iGame.060")
		)
	)
)

(if (= #detectedSystem "os4")
    (
        (set #selectedVersion "iGame.OS4")
    )
)
(if (= #detectedSystem "mos")
    (
        (set #selectedVersion "iGame.MOS")
    )
)

;=============================================================================
; Select destination directory
(complete 20)

(set seldir 1)

(if (<> seldir 0)
	(set #selectedDest
		(askdir
			(prompt #destdir)
			(help @askdir-help)
			(default (pathonly #defaultDest))
		)
	)
	(set #selectedDest
		(expandpath #selectedDest)
	)
)

(set #destination (tackon #selectedDest "iGame"))

(if (<> seldir 0)
	(
		(if (<> (exists #destination) 0)
		(
			(message (cat #destexists_1 #selectedDest #destexists_2))
		)
		)
	)
)

;=============================================================================
; Select language
(complete 30)

(set #catalog_lang
	(askoptions
		(prompt #lang_catalog)
		(help @askoptions-help)
		(choices
			(if (in #supportedLanguages #lang_english)
				(cat #prop "English")
				("")
			)
			(if (in #supportedLanguages #lang_french)
				(cat #prop "Fran�ais")
				("")
			)
			(if (in #supportedLanguages #lang_german)
				(cat #prop "Deutsch")
				("")
			)
			(if (in #supportedLanguages #lang_greek)
				(cat #prop "Greek")
				("")
			)
			(if (in #supportedLanguages #lang_italiano)
				(cat #prop "Italiano")
				("")
			)
			(if (in #supportedLanguages #lang_portuguese)
				(cat #prop "Portuguese")
				("")
			)
			(if (in #supportedLanguages #lang_turkish)
				(cat #prop "T�rk�e")
				("")
			)
		)
		(default #defaultLanguages)
	)
)

;=============================================================================
; Create the destination folder
(complete 40)

(if (= (exists #destination) 0)
	(
		(makedir #destination
			(prompt (cat "iGame will be installed at\n" #destination))
			(help @makedir-help)
			(confirm "expert")
		)
	)
)

;=============================================================================
; Create and copy Extras folder
(complete 50)

(makedir (tackon #destination "Extras")
	(prompt (cat "Extra Extras will be installed at\n" (tackon #destination "Extras")))
	(help @makedir-help)
	(confirm "expert")
)
(foreach "Extras" "#?.png"
	(copyfiles
		(source (tackon "Extras" @each-name))
		(dest (tackon #destination "Extras"))
		(help @copyfiles-help)
	)
)
(foreach "Extras/Icons" "#?.info"
	(copyfiles
		(source (tackon "Extras/Icons" @each-name))
		(dest (tackon #destination "Extras/Icons"))
		(help @copyfiles-help)
	)
)

;=============================================================================
; Copy Guide files and logo
(complete 60)

(copyfiles
	(prompt (cat "Should iGame.guide be installed at\n" #destination "?"))
	(source "iGame.guide")
	(dest #destination)
	(help @copyfiles-help)
	(confirm "expert")
)
(copyfiles
	(source "iGame.guide.info")
	(dest #destination)
	(help @copyfiles-help)
)
(copyfiles
	(source "logo.iff")
	(dest #destination)
	(help @copyfiles-help)
)

;=============================================================================
; Copy default iff image
(complete 70)

(copyfiles
	(prompt (cat "Should igame.iff be installed at\n" #destination "?"))
	(source "igame.iff")
	(dest #destination)
	(help @copyfiles-help)
	(confirm "expert")
)

;=============================================================================
; Copy locale catalog files
(complete 80)

; check if there were any catalogs selected
(if (<> 0 #catalog_lang)
	(
		(makedir (tackon #destination "catalogs")
			(help @makedir-help)
		)

		; copy the .cd files
		(foreach "catalogs" "#?.cd"
			(copyfiles
				(source (tackon "catalogs" @each-name))
				(dest (tackon #destination "catalogs"))
				(help @copyfiles-help)
			)
		)

		; greek catalog file
		(if (in #catalog_lang #lang_greek)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_greek)))

				(foreach "catalogs/greek" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/greek" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_greek)))
						(help @copyfiles-help)
					)
				)
			)
		)

		; italiano catalog file
		(if (in #catalog_lang #lang_italiano)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_italiano)))

				(foreach "catalogs/italiano" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/italiano" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_italiano)))
						(help @copyfiles-help)
					)
				)
			)
		)

		; portuguese catalog file
		(if (in #catalog_lang #lang_portuguese)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_portuguese)))

				(foreach "catalogs/portuguese" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/portuguese" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_portuguese)))
						(help @copyfiles-help)
					)
				)
			)
		)

		; turkish catalog file
		(if (in #catalog_lang #lang_turkish)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_turkish)))

				(foreach "catalogs/turkish" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/turkish" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_turkish)))
						(help @copyfiles-help)
					)
				)
			)
		)

		; french catalog file
		(if (in #catalog_lang #lang_french)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_french)))

				(foreach "catalogs/french" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/french" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_french)))
						(help @copyfiles-help)
					)
				)
			)
		)

		; german catalog file
		(if (in #catalog_lang #lang_german)
			(
				(makedir (tackon #destination (tackon "catalogs" #cat_name_german)))

				(foreach "catalogs/german" "#?.catalog"
					(copyfiles
						(source (tackon "catalogs/german" @each-name))
						(dest (tackon #destination (tackon "catalogs" #cat_name_german)))
						(help @copyfiles-help)
					)
				)
			)
		)
	)
)

;=============================================================================
; Copy default genres
(complete 90)

(copyfiles
	(prompt (cat "Should default genres list file be installed at\n" #destination "?"))
	(source "genres")
	(dest #destination)
	(help "This step will copy the genres default list file at the destination folder. If you have your own list, with extra genres added, you might want to skip this step. Otherwise, proceed with the copy.")
	(confirm "average")
)

;=============================================================================
; Copy main executable
(complete 95)

(copyfiles
	(prompt (cat "Should iGame be installed at\n" #destination "?"))
	(source #selectedVersion)
	(dest #destination)
	(newname "iGame")
	(help "This step will copy the iGame executable at the destination folder. If this is the first time you install iGame or you want to update an older version, proceed with the copy.")
	(confirm "expert")
)
(copyfiles
	(prompt (cat "Should iGame.info be installed at\n" #destination "?"))
	(source "iGame.info")
	(dest #destination)
	(help "This step will copy the iGame.info file at the destination folder. If you update an older version and you have already changed the default tooltypes, you might want to skip this step. Otherwise, proceed with the copy.")
	(confirm "average")
)

;=============================================================================
; Final message
(complete 100)

(message #done)

(exit (QUIET))
